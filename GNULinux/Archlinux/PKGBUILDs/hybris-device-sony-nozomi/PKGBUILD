# Maintainer: MichaÃ«l Serpieri <mickybart@pygoscelis.org>

# Remarks about /home rule violation (see https://wiki.archlinux.org/index.php/Arch_packaging_standards#Directories)
#
# Android needs to generate a lot of files under /data that will be untracked.
# /data is used for large purpose included media files of the user.
# So /data should fit better under /home than /opt/android/rootfs/data
# We absolutely need to be sure that /home/android is owned by us to don't break anything

pkgname=hybris-device-sony-nozomi
pkgver=5.1.1_30
pkgrel=1
pkgdesc="hybris - Android for Sony Xperia S (nozomi)"
arch=('armv7h')
url="http://forum.xda-developers.com/xperia-s/s-development/rom-naosprom-xperia-s-t2958516"
license=('custom')
provides=('hybris-device')
depends=('schroot-hybris' 'systemd-legacy' 'hybris-linux-sony-nozomi')
options=('!strip')
install='hybris-device.install'
source=(hybris-device.install
	generate-android-udev-rules.sh
        generate-android-users.sh
	hybris-device.install.template
        default.hybris-device
	profile.hybris-device.sh
	hybris-device.tgz)
md5sums=('SKIP'
	 'b128af77ca5d2e1c8eeda192baa7b298'
	 'abf104cdf8a37a1d3809409666ef29ea'
	 '69e168c70e61e19fbf86217fa691c084'
	 '96a74b7fef30065e22906e4a63fe91fa'
	 '75e4bba978030127135ac0bff97960a1'
	 'SKIP')
_product_out=out/target/product/$(echo $pkgname | cut -d'-' -f4)

build() {
  cd "${srcdir}"

  # Generate udev rules source files

  sh generate-android-udev-rules.sh ./${_product_out}/root

  # Generate groups and users creation based on ./headers
  # and create the future .INSTALL of the package
  #
  # WARNING: before publishing the PKGBUILD sources,
  #          take care to cleanup hybris-device.install
  #          with '> hybris-device.install'
  #
  # NOTE: some users are specific per device and android version
  #       so for now we handle EVERY users creation in hybris-device
  #       as part of the portage effort

  sh generate-android-users.sh ./${_product_out} > hybris-device.install
  cat hybris-device.install.template >> hybris-device.install
}

package() {
  cd "${srcdir}"

  # Android ramdisk (rootfs)
  #

  install -d "${pkgdir}/opt/android"
  cp -a ./${_product_out}/root "${pkgdir}/opt/android/rootfs"
  chown -R root:root "${pkgdir}/opt/android/rootfs/"

  # Android system
  #

  rm -rf "${pkgdir}/opt/android/system"
  cp -a ./${_product_out}/system "${pkgdir}/opt/android/system"
  chown -R root:root "${pkgdir}/opt/android/system/"

  # Android /data
  #
  install -d "${pkgdir}/home"
  install -d -m 771 -g 1000 -o 1000 "${pkgdir}/home/android"

  # Android integration (needed for libhybris / proprietary lib)
  #
  ln -s /opt/android/system "${pkgdir}/system"
  ln -s /opt/android/system/vendor "${pkgdir}/vendor"

  # Android firmware files
  #
  # NOTE: 'systemd-legacy' needed for userspace firmware loading.
  #       If you don't need to load firmware from userspace,
  #       you can switch dependency to 'systemd'

  install -d "${pkgdir}/usr/lib/firmware/"
  cp -r "${pkgdir}"/opt/android/system/etc/firmware/* "${pkgdir}/usr/lib/firmware/"
  cp -r "${pkgdir}"/opt/android/system/vendor/firmware/* "${pkgdir}/usr/lib/firmware/"

  # Android headers
  #
  # NOTE: needed to compil libhybris-git, qpa-qt-hwcomposer-plugin, ...

  cd "$srcdir/${_product_out}/headers"
  make DESTDIR="$pkgdir/" PREFIX=/opt/android INCLUDEDIR=/opt/android/include install
  cd "${srcdir}"
  install -d "${pkgdir}/usr/lib/pkgconfig"
  mv "${pkgdir}/opt/android/include/android-headers.pc" "${pkgdir}/usr/lib/pkgconfig/"
  sed -i 's|Cflags:.*|Cflags: -I/opt/android/include|' "${pkgdir}/usr/lib/pkgconfig/android-headers.pc"

  # udev rules for the device
  #
  # NOTE: review generated rules and patch them here if needed.
  #       Thanks to report your changes to permit us
  #       to fix generate-android-udev-rules.sh script

  install -d "${pkgdir}/usr/lib/udev/rules.d/"
  cp *.rules "${pkgdir}/usr/lib/udev/rules.d/"

  # Configuration file
  #
  # NOTE: permit to set variables used to configure
  #       backend, screen parameters, etc for external components

  install -d "${pkgdir}/etc/default"
  install -m 644 default.hybris-device "${pkgdir}/etc/default/hybris-device"

  install -d "${pkgdir}/etc/profile.d"
  install -m 755 profile.hybris-device.sh "${pkgdir}/etc/profile.d/hybris-device.sh"

  # schroot environment
  #
  # NOTE: default configuration should be good but
  #       just in case, we prefer to handle it on this package
  #       if adaptation is needed about mount, shell etc

  install -d "${pkgdir}/etc"
  cp -r /usr/share/schroot/examples/android "${pkgdir}/etc/schroot"

}

